// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {
    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Keyboard.init();
        do Output.init();

        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while (true) {
            do Sys.wait(1);
        }
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i, j, delay;
        let delay = 1000;
        while (i < duration) {
            let j = 0;
            while (j < delay) {
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        var String s;

        let s = "ERR";
        do Output.printString(s);
        do Output.printInt(errorCode);
        do s.dispose();
        do Sys.halt();
        return;
    }
}
